group = "com.golanpiyush.flutter_ytdlp_plugin"
version = "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "2.1.0"
    repositories {
        google()
        mavenCentral()
        maven { url "https://chaquo.com/maven" }
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.7.3")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
        classpath "com.chaquo.python:gradle:15.0.1"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://chaquo.com/maven" }
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: 'com.chaquo.python'

android {
    namespace = "com.golanpiyush.flutter_ytdlp_plugin"
    compileSdk = 35
    
    sourceSets {
        main {
            java.srcDirs += "src/main/kotlin"
            python.srcDir "src/main/python"
        }
        test.java.srcDirs += "src/test/kotlin"
    }
    
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
    
    defaultConfig {
        minSdk = 24
        targetSdk = 35
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }
    
    buildTypes {
        release {
            minifyEnabled false
        }
    }
   
    
    testOptions {
        unitTests.all {
            useJUnitPlatform()
            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}

chaquopy {
    defaultConfig {
        version "3.11"
        buildPython "C:\\Python311\\python.exe"  
        pip {
            install "yt-dlp==2025.07.21"
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // implementation 'com.arthenica:ffmpeg-kit-full:6.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3"
    // implementation "com.fasterxml.jackson.core:jackson-databind:2.14.3"
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.10.0")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.10.0")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.10.0")
    testImplementation("org.mockito:mockito-core:5.2.0")
    
    testImplementation("org.jetbrains.kotlin:kotlin-test")
    
}